# $Id: PKGBUILD 281258 2016-11-18 20:36:27Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop avogadrolibs depend for now: GL-only

pkgname=kalzium
pkgver=16.12.0
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="http://kde.org/applications/education/kalzium/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(hicolor-icon-theme khtml kdelibs4support knewstuff kplotting)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin python eigen) # ocaml facile
conflicts=(kdeedu-kalzium)
replaces=(kdeedu-kalzium)
groups=(kde-applications kdeedu)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha1sums=('2e5a1b59149c715e5fe534689625f425fd6443d2'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
