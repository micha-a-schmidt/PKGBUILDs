# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - ARM patch, use NO_CRASH_HANDLER=1 for AArch64

pkgname=texstudio
pkgver=2.11.2
pkgrel=2
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('i686' 'x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL')
depends=('poppler-qt5' 'qt5-svg' 'qt5-script' 'libxkbcommon-x11')
makedepends=('qt5-tools' 'mercurial')
optdepends=('evince: pdf reader'
            'okular: alternate pdf reader')
replaces=('texmakerx')
source=("hg+http://hg.code.sf.net/p/texstudio/hg#tag=${pkgver}"
        texstudio-debughelper-arm-fix.patch)
md5sums=('SKIP'
         '54eba96da2c93980fd9d85f751a56905')

prepare() {
    cd hg
    patch -p1 -i ../texstudio-debughelper-arm-fix.patch
}

build() {
    cd hg
    [[ $CARCH == "aarch64" ]] && EXTRA="NO_CRASH_HANDLER=1"
    qmake-qt5 CONFIG-="debug" $EXTRA texstudio.pro
    make
}

package() {
    cd hg
    make INSTALL_ROOT="$pkgdir" install
}
